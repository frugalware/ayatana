# Compiling time: 1.67 SBU
# Maintainer: bouleetbil <bouleetbil@frogdev.info>
# Contributor: Bence Nagy <nagybence@tipogral.hu>

pkgname=claws-mail
pkgver=3.7.9
pkgrel=4
pkgdesc="The extended version of Sylpheed, a GTK+ based, lightweight, and fast e-mail client"
groups=('xapps-extra')
archs=('i686' 'x86_64' 'ppc')
options=('scriptlet')
depends=('libxml2>=2.7.8' 'openssl>=1.0.0' 'aspell' 'pilot-link>=0.12.1'  \
	'gtk+2>=2.20.0-2' 'gpgme' 'startup-notification' 'libldap' \
	'spamassassin' 'curl' 'libetpan>=1.1' 'gnutls>=2.8' \
	'hicolor-icon-theme' 'glib2>=2.18.4-1' 'db>=4.7.25')
makedepends=('openldap>=2.4.22-2')
_F_sourceforge_realname="Claws Mail"
_F_sourceforge_ext=".tar.bz2"
_F_sourceforge_dirname="sylpheed-claws"
conflicts=('sylpheed-claws' 'claws-mail-plugin-smime')
replaces=('sylpheed-claws' 'claws-mail-plugin-smime' 'claws-webkit-plugin' 'claws-mail-plugins-maildir')
provides=('sylpheed-claws' 'claws-mail-plugin-smime')
Finclude sourceforge
source=(${source[@]} claws-mail-3.7.9_notify_crash.patch)
url="http://www.claws-mail.org/"

build() {
	Fpatchall
	Fmake \
		--enable-aspell --enable-ldap \
		--enable-spamassassin-plugin
	cd tools || Fdie
	make || Fdie
	for tool in *.pl *.py *.sh tb2* update-po uudec; do
    		Fexerel /usr/lib/claws-mail/tools/${tool}
	done
	Ffilerel /usr/lib/claws-mail/tools/multiwebsearch.conf
	cd .. || Fdie
	Ffilerel claws-mail-64x64.png /usr/share/pixmaps/claws-mail.png
	Fmakeinstall
	Ffilerel claws-mail.png /usr/share/icons/hicolor/48x48/apps/claws-mail.png
	Ffilerel claws-mail.desktop /usr/share/applications/claws-mail.desktop
}

sha1sums=('a5d5d93d5d6d6cabdbabd391724d7564a304449a' \
          '961eb6e40eea90bac8596b239517bc3bd1f71be7')

# optimization OK
